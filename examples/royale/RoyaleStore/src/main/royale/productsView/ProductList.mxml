<?xml version="1.0" encoding="utf-8"?>
<!--

Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-->
<js:Container xmlns:fx="http://ns.adobe.com/mxml/2009" 
                 xmlns:js="library://ns.apache.org/royale/basic" 
    > 
    <js:style>
        <js:SimpleCSSStyles borderStyle="solid" borderWidth="1" backgroundColor="#BCB29F" />
    </js:style>
    <!-- need a background color for drag and drop but can set alpha to 0 -->
    
    <fx:Metadata>
       [Event(name="addProduct", type="samples.royalestore.ProductListEvent")]
       [Event(name="duplicateProduct", type="samples.royalestore.ProductListEvent")]
       [Event(name="productQtyChange", type="samples.royalestore.ProductListEvent")]
       [Event(name="removeProduct", type="samples.royalestore.ProductListEvent")]
    </fx:Metadata>

    <fx:Script>
        <![CDATA[
        import org.apache.royale.effects.Effect;
        import org.apache.royale.effects.Move;
        import org.apache.royale.effects.Sequence;
        import org.apache.royale.core.DropType;
        import org.apache.royale.core.IUIBase;
        import org.apache.royale.events.DragEvent;
        
        import samples.royalestore.Product;
        import samples.royalestore.ProductListEvent;
        
        public var items:Array;
        
        public var newItemStartX:int;
        public var newItemStartY:int;
        [Bindable]
        public var maxItems:int = 0;
        public var showQuantity:Boolean;
        
        private var playingEffects:Object = new Object();
        
        public function addProduct(product:Product):void
		{
            if (items == null)
                items = [];
            
            var index:int = indexOf(product.productId);
            var event:ProductListEvent;
            var item:ProductListItem;
            
            if (index != -1)
			{
			    item = items[index] as ProductListItem;
			    //if we don't keep track of what's playing a double-click can
			    //cause the list item to keep rising
			    if (playingEffects[item.uid] == null)
			    {
                    var jump:Sequence = new Sequence();
                    var m1:Move = new Move(item)
                    m1.yBy = -5;
                    var m2:Move = new Move(item)
                    m2.yBy = 5;
                    jump.addChild(m1);
                    jump.addChild(m2);
                    jump.duration = 150;
                    playingEffects[item.uid] = jump;
                    jump.addEventListener(Effect.EFFECT_END, function(event:Event):void
                    {
                       delete playingEffects[item.uid];
                    });
                    jump.play();
                }
                event = new ProductListEvent(ProductListEvent.DUPLICATE_PRODUCT);
                event.product = item.product;
                dispatchEvent(event);
            }
			else
			{
                index = items.length;
                if (maxItems <= 0 || index < maxItems)
				{
				    item = new ProductListItem();
				    if (showQuantity)
				    {
				        item.currentState = 'showQuantity';
				    }
				    item.product = product;
				    item.percentWidth = 100;
				    item.addEventListener(ProductListEvent.REMOVE_PRODUCT, removeItemHandler);
                    items[index] = item;
                    viewport.addElement(item);
                    layoutItems(index, true);
                    event = new ProductListEvent(ProductListEvent.ADD_PRODUCT);
                    event.product = product;
                    dispatchEvent(event);
                }
            }
        }
        
        public function getProducts():Array
        {
            var ret:Array = [];
            for (var i:int = 0; i < items.length; i++)
            {
                ret[i] = ProductListItem(items[i]).product;
            }
            return ret;
        }
        
        private function removeItemHandler(event:Event):void
        {
            var item:ProductListItem = event.target as ProductListItem;
            var index:int = indexOf(item.product.productId);
            items.splice(index, 1);
            viewport.removeElement(item);
            layoutItems(index);
        }
            
        private function layoutItems(startIndex:int, scrollToBottom:Boolean=false):void
		{
            var n:int = items.length;
            var e:Move;
			for (var i:int = startIndex; i < n ; i++)
			{
			    var item:ProductListItem = items[i];
                var yTo:Number = i * (item.height);
                //still need to prevent items that are already in motion from getting
                //jumpy
			    if (playingEffects[item.uid] == null)
			    {
                    e = new Move(item);
                    if (item.x == 0 && item.y == 0)
    				{
    					e.xFrom = newItemStartX;
    					e.yFrom = newItemStartY;
                    }
    
                    e.xTo = 0;
                    e.yTo = yTo;
                    playingEffects[item.uid] = e;
                    e.addEventListener(Effect.EFFECT_END, function(event:Event):void
                    {
                       delete playingEffects[item.uid];
                    });
                    e.play();
                }
                else
                {
                    playingEffects[item.uid].pause();
                    playingEffects[item.uid].yTo = yTo;
                    playingEffects[item.uid].play();
                }
            }
            //get the last event and if we should scroll make sure we can validate
            //and scroll to maxVPosition
            if (scrollToBottom)
            {
                e.addEventListener(Effect.EFFECT_END, function(event:Event):void
                {
  //TODO                  scrollingView.model.verticalScrollPosition = scrollingView.maxVerticalScrollPosition;    
                });
            }
        }
        
        private function indexOf(productId:int):int
		{
            var index:int = -1;

            var n:int = items.length;
			for (var i:int = 0; i < items.length; i++)
			{
                if (ProductListItem(items[i]).product.productId == productId)
				{
                    index = i;
                    break;
                }
            }

            return index;
        }     

        private function doDragEnter(event:DragEvent):void
        {
            trace("doDragEnter");
            dmc.acceptDragDrop(event.target as IUIBase, DropType.COPY);
        }

        private function doDragDrop(event:DragEvent):void
        {
            trace("doDragDrop");
            var product:Product = DragEvent.dragSource as Product;
            addProduct(product);
        }  
 
        ]]>
    </fx:Script>
    <js:beads>
        <js:DropMouseController id="dmc" dragEnter="doDragEnter(event)" dragDrop="doDragDrop(event)" />
        <js:VerticalLayout />
		<js:ScrollingViewport id="scrollingView" />
    </js:beads>
    <js:Container id="viewport" width="100%" height="100%" >
    </js:Container>
</js:Container>
